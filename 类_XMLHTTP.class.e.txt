.版本 2

.程序集 类_XMLHTTP, , 公开
.程序集变量 对象, 对象

.子程序 _初始化
    CoInitialize (0)
    创建 ()

.子程序 _销毁
    CoUninitialize ()

.子程序 创建, 逻辑型
    .如果真 (对象.创建 (“MSXML2.XMLHTTP”, ) ＝ 假)
        返回 (假)
    .如果真结束
    返回 (真)

.子程序 打开, 逻辑型, 公开, 打开HTTP连接
    .参数 模式, 文本型, , 模式为POST GET PUT PROPFIND之一
    .参数 地址, 文本型, , 地址
    .参数 异步方式, 逻辑型, 可空, 是否为异步方式
    .参数 用户名, 文本型, 可空, 用户名
    .参数 密码, 文本型, 可空, 密码

    对象.方法 (“open”, 模式, 地址, 异步方式, 用户名, 密码)
    返回 (真)

.子程序 指定请求头数据, , 公开, 单独指定请求的http头
    .参数 头名称, 文本型, , 头名称
    .参数 值, 文本型, , 值

    对象.方法 (“setRequestHeader”, 头名称, 值)

.子程序 取响应头数据, 文本型, 公开, 从响应信息中获取指定的http头
    .参数 头标志, 文本型, , 头标志

    返回 (对象.文本方法 (“getResponseHeader”, 头标志))

.子程序 取所有头数据, 文本型, 公开, 获取响应的所有http头
    返回 (对象.文本方法 (“getAllResponseHeaders”, ))

.子程序 发送请求, , 公开, 发送请求
    .参数 发送数据, 文本型, 可空, 发送的数据

    对象.方法 (“send”, 发送数据)

.子程序 取消当前请求, , 公开, 取消当前请求
    对象.方法 (“abort”, )

.子程序 取状态码, 整数型, 公开, 返回当前请求的http状态码
    返回 (对象.读数值属性 (“status”, ))

.子程序 取状态文本, 文本型, 公开, 返回当前请求的状态文本
    返回 (对象.读文本属性 (“statusText”, ))

.子程序 取responseXML, 对象, 公开, 将响应信息格式化为 Xml 文档对象并返回
    返回 (对象.读对象型属性 (“responseXML”, ))

.子程序 取responseText, 文本型, 公开, 将响应信息作为字符串返回
    返回 (对象.读文本属性 (“responseText”, ))

.子程序 取响应数据, 变体型, 公开, 返回服务器响应数据
    返回 (对象.读属性 (“responseBody”, ))

.子程序 取responseStream, 变体型, 公开, 以Ado Stream对象的形式返回响应信息
    返回 (对象.读属性 (“responseStream”, ))

.子程序 取readyState, 整数型, 公开, 返回请求的当前状态
    返回 (对象.读数值属性 (“readyState”, ))

.子程序 置onreadystatechange, , 公开, 指定当readyState属性改变时的子程序指针
    .参数 参数值, 子程序指针

    对象.写属性 (“onreadystatechange”, 参数值)

